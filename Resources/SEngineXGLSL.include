
struct DirectionalLight {
    vec4 color;
    vec4 direction;
};

struct PointLight {
    vec4 position;
    vec4 color;
    float constant;
    float linear;
    float quadratic;
 //   float pad;
};

#define NR_POINT_LIGHTS 4

layout (std140) uniform InternalData
{
    vec4 Ambient;
    vec4 ViewPos;
    PointLight pointLights[4];
    DirectionalLight directionalLights[2];
    float NumberOfPointLights;
    float NumberOfDirectionalLights;
    mat4 DirLightSpace;
} Lights;

struct LightingResult {
    vec3 diffuse;
    vec3 specular;
};

uniform sampler2D _DirLightDepth;

float DirShadowCalculation(vec4 fragPosLightSpace)
{
  //perspective divide
   vec3 projCoords = fragPosLightSpace.xyz / fragPosLightSpace.w;
    
    projCoords = projCoords * 0.5 + 0.5;
    float closestDepth = texture(_DirLightDepth, projCoords.xy).r;
    // Get depth of current fragment from light's perspective
    float currentDepth = projCoords.z;
    // Check whether current frag pos is in shadow
    float shadow = currentDepth > closestDepth  ? 1.0 : 0.0;
    
    return shadow;
}

LightingResult _Lighting_Directional(DirectionalLight light, vec3 normal, vec3 viewDir)
{
    LightingResult result;
    
    vec3 lightDir = normalize(-light.direction.xyz);
    
    // Diffuse shading
    float diff = max(dot(normal, lightDir), 0.0);
    
    // Specular shading
    vec3 halfwayDir = normalize(lightDir + viewDir);
    float spec = pow(max(dot(normal, halfwayDir), 0.0), material.Shininess);
    
    float shadow = DirShadowCalculation(vFragDirLightPosition);
    
    result.diffuse = light.color.xyz * diff * (1.0 - shadow);
    result.specular = light.color.xyz * spec * (1.0 - shadow);
    
    return result;
}

// Calculates the color when using a point light.
LightingResult _Lighting_Point(PointLight light, vec3 normal, vec3 fragPos, vec3 viewDir)
{
    LightingResult result;
    
    vec3 lightDir = normalize(light.position.xyz - fragPos);
    
    // Diffuse shading
    float diff = max(dot(normal, lightDir), 0.0);
    // Specular shading
    vec3 halfwayDir = normalize(lightDir + viewDir);
    float spec = pow(max(dot(normal, halfwayDir), 0.0), material.Shininess);
    
    // Attenuation
    float distance = length(light.position.xyz - fragPos);
    float attenuation = 1.0f / (light.constant + light.linear * distance + light.quadratic * (distance * distance));
    // Combine results
    
    result.diffuse = light.color.xyz * diff * attenuation;
    result.specular = light.color.xyz * spec * attenuation;
    
    return result;
}